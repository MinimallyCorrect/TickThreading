buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "nallar"
			url = "https://repo.nallar.me/"
		}
	}
	if (System.getenv("GRADLE_NO_CACHE_CHANGING") != null) {
		configurations.all {
			resolutionStrategy.cacheChangingModulesFor 1, 'seconds'
			resolutionStrategy.cacheDynamicVersionsFor 1, 'seconds'
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
		classpath 'org.minimallycorrect.modpatcher:ModPatcherGradle:0.1-SNAPSHOT'
		classpath 'org.minimallycorrect.libloader:LibLoaderGradle:0.1-SNAPSHOT'
	}
}
apply plugin: 'org.minimallycorrect.modpatcher.ModPatcherGradle'
apply plugin: 'org.minimallycorrect.libloader.LibLoaderGradle'

group = 'org.minimallycorrect.tickthreading'
archivesBaseName = 'TickThreading'

// Extended project information
ext.projectName = project.name
ext.modid = project.name.toLowerCase()
ext.inceptionYear = '2014'
ext.packaging = 'jar'
ext.url = 'https://github.com/MinimallyCorrect/TickThreading'
ext.description = 'TickThreading'
ext.organization = 'minimallycorrect.org'
ext.mcVersion = '1.10.2'
ext.forgeVersion = "12.18.3.2281"
ext.fullForgeVersion = mcVersion + "-" + forgeVersion

gradle.startParameter.showStacktrace = org.gradle.api.logging.configuration.ShowStacktrace.ALWAYS

// Define variables
ext.buildNumber = System.getenv("BUILD_NUMBER") ?: 0
ext.ciSystem = System.getenv("JENKINS_URL") ? 'Jenkins' : 'unknown'
ext.commit = System.getenv("GIT_COMMIT") ?: 'unknown'

version = mcVersion + '.' + buildNumber

minecraft {
	version = fullForgeVersion
	mappings = "snapshot_20161111"

	replace "@MOD_NAME@", project.name
	replace "@MOD_ID@", modid
	replace "@MOD_VERSION@", project.version
	replace "@MC_VERSION@", version
}

modpatcher {
	mixinPackage = "org.minimallycorrect.tickthreading.mixin"
	extractGeneratedSources = true
	generateInheritanceHierarchy = true
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
defaultTasks "setupCiWorkspace", "build"

repositories {
	jcenter()
	maven { url 'https://repo.nallar.me/' }
}

if (System.getenv("GRADLE_NO_CACHE_CHANGING") != null) {
	configurations.all {
		resolutionStrategy.cacheChangingModulesFor 1, 'seconds'
		resolutionStrategy.cacheDynamicVersionsFor 1, 'seconds'
	}
}

dependencies {
	testCompile 'junit:junit:4.12'
	compileOnly 'org.projectlombok:lombok:1.16.12'
	libLoader "org.minimallycorrect.typedconfig:TypedConfig:0.1-SNAPSHOT"
	libLoader "org.minimallycorrect.modpatcher:ModPatcher:$mcVersion-SNAPSHOT"
}

if (System.env.GRADLE_USER_HOME) {
	ext.homeDir = System.env.GRADLE_USER_HOME + '/'
} else {
	ext.homeDir = System.properties['user.home'] + '/.gradle/'
}
ext.mappingsPath = homeDir + 'caches/minecraft/net/minecraftforge/forge/' + fullForgeVersion + '/unpacked/conf/'

task extendedJar(type: Jar, dependsOn: classes)

def jarConfig = {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	manifest {
		attributes("Built-By": System.properties['user.name'],
			"Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
			"Implementation-Title": name,
			"Implementation-Version": version + "+" + ciSystem + "-b" + buildNumber + ".git-" + commit,
			"Implementation-Vendor": url,
			"FMLCorePlugin": "org.minimallycorrect.tickthreading.mod.TickThreadingCore",
			"FMLCorePluginContainsFMLMod": "true"
		)
	}
	from mappingsPath + 'packaged.srg'
	from mappingsPath + 'methods.csv'
	from mappingsPath + 'fields.csv'
	from './generated/extendsMap.obj'
}

jar jarConfig
extendedJar jarConfig

jar {
	classifier = 'core'
	exclude('org/minimallycorrect/tickthreading/mixin/extended**')
}

extendedJar {
	classifier = 'extended'
	from {
		sourceSets.main.allSource
		sourceSets.main.output
	}
}

artifacts {
	archives jar
	archives extendedJar
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'version': project.version, 'mcversion': project.minecraft.version, 'name': project.projectName, 'modid': modid
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) { exclude 'mcmod.info' }
}

// Source compiler configuration
tasks.withType(JavaCompile) {
	sourceCompatibility = 8
	targetCompatibility = 8
	options.with {
		deprecation = true
		encoding = 'UTF-8'
		compilerArgs <<
			"-XDignore.symbol.file=true" <<
			"-Xlint:all" <<
			"-Xlint:-path" <<
			"-Xlint:-processing"
		fork = true
		forkOptions.executable = 'javac'
	}
}

wrapper {
	gradleVersion = "3.5"
}
